/*
 * Copyright 2019, Perfect Sense, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'maven-publish'
    id 'java'
    id 'com.github.johnrengelman.shadow' version "4.0.3"
    id 'checkstyle'
}

version = "0.99.0-SNAPSHOT"

defaultTasks 'shadowJar'

if (System.getenv("TRAVIS_BRANCH") == null) {
    check.dependsOn 'publishToMavenLocal'
    publishToMavenLocal.dependsOn 'shadowJar'
} else {
    check.dependsOn 'publish'
}

tasks.jar.configure {
    classifier = 'default'
}

assemble.dependsOn shadowJar

sourceCompatibility = 1.8

dependencies {
    compile "com.google.guava:guava:23.0"
    compile 'com.psddev:dari-util:3.3.607-xe0f27a'
    compile "gyro:gyro-core:0.99.0-SNAPSHOT"
    compile files("${System.getProperty('java.home')}/../lib/tools.jar")

    checkstyle 'com.puppycrawl.tools:checkstyle:8.15'
}

repositories {
    mavenLocal()
    mavenCentral()

    maven {
        url "https://artifactory.psdops.com/public"
    }
    maven {
        url "https://artifactory.psdops.com/gyro-snapshots"
    }
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            groupId = 'gyro'
            artifactId = 'gyro-doclet'
        }
    }

    repositories {
        maven {
            credentials {
                username System.getenv("GRADLE_DEPLOY_USER")
                password System.getenv("GRADLE_DEPLOY_PASSWORD")
            }

            if (project.version.endsWith('-SNAPSHOT')) {
                url "https://artifactory.psdops.com/gyro-snapshots"
            } else {
                url "https://artifactory.psdops.com/gyro-releases"
            }
        }
    }
}

checkstyle {
    toolVersion = '6.13'
    configFile rootProject.file('config/checkstyle.xml')
    checkstyleMain.enabled = false
    checkstyleTest.enabled = false
}

task checkstyle(type: Checkstyle) {
    configFile rootProject.file('config/checkstyle.xml')
    source 'src'
    include '**/*.java'
    exclude '**/generated-src/**'

    classpath = files()
}

shadowJar {
    baseName = 'gyro-doclet'
    classifier = ''
}
